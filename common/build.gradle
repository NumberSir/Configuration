plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version "${vanilla_gradle_version}"
    id 'maven-publish'
}

version = "${project.version}"
group = project.mod_group
def classifier = 'common'
base {
    archivesName = "${project.mod_id}-${classifier}-${project.minecraft_version}"
}

minecraft {
    version("${minecraft_version}")

    def accessWidenerFile = file("src/main/resources/${mod_id}.accesswidener")
    if (accessWidenerFile.exists()) {
        accessWideners(accessWidenerFile)
    }

    runs {
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = "${project.mod_id}-${project.minecraft_version}"
            version = "${project.version}-${classifier}"
            from components.java
        }
    }

    repositories {
        maven {
            url = uri(project.findProperty("MAVEN_PUBLIC_URL") ?: "")
            credentials {
                username project.findProperty("MAVEN_USERNAME") ?: ""
                password project.findProperty("MAVEN_PASSWORD") ?: ""
            }
        }
    }
}